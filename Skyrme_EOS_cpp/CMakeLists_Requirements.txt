# GLOBAL REQUIREMENTS
#
# These packages are always required

find_package(HDF5 REQUIRED CXX)
set(EOSBUILDER_EXTERNAL_LIBS "${EOSBUILDER_EXTERNAL_LIBS};${HDF5_LIBRARIES}")
include_directories(${HDF5_INCLUDE_DIRS})

find_package(GSL REQUIRED)
set(EOSBUILDER_EXTERNAL_LIBS "${EOSBUILDER_EXTERNAL_LIBS};${GSL_LIBRARIES}")
include_directories(${GSL_INCLUDE_DIRS})

# Git (>= 1.7) for git rev-parse --show-toplevel
find_package(Git 1.7 REQUIRED)

find_package(Boost REQUIRED system filesystem serialization)
set(EOSBUILDER_EXTERNAL_LIBS "${EOSBUILDER_EXTERNAL_LIBS};${Boost_LIBRARIES}")
include_directories(${Boost_INCLUDE_DIRS})

# SWIG (optional)
#
# These packages are required to build the Python bindings with SWIG

if(REQUIRE_SWIG)
  # REQUIRE_SWIG implies ENABLE_SWIG
  unset(ENABLE_SWIG CACHE)
endif()

option(ENABLE_SWIG "Tell cmake to enable support for package SWIG" ON)

if(ENABLE_SWIG)
  option(REQUIRE_SWIG "Abort the build if SWIG is not found." OFF)
  find_package(SWIG 3)
  find_package(PythonLibs 2.6)

  #TODO is there a better way to do this?
  if(SWIG_FOUND AND PYTHONLIBS_FOUND)
    set(USE_SWIG TRUE)
  else()
    set(USE_SWIG FALSE)
  endif()
else()
  unset(REQUIRE_SWIG CACHE)
  set(USE_SWIG FALSE)
endif()

if(REQUIRE_SWIG AND (NOT USE_SWIG))
  message(FATAL_ERROR "Requested support for SWIG, but SWIG and/or PythonLibs could not be found.")
endif()

if(USE_SWIG)
  include(${SWIG_USE_FILE})
  include_directories(${PYTHON_INCLUDE_DIRS})
endif()



